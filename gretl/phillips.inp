set verbose off
set workdir ./build

open ../data/phillips.csv

ldiff defl
infl = 100 * ld_defl

paroc = hpfilt(paro, 6.25)

diff infl
diff paroc

series covid = obs == obsnum(2020)

smpl 1965 2023

gnuplot infl --time-series --with-lines --output=phillips-infl.pdf
outfile phillips-adf-infl.txt
  adf 3 infl --c --test-down=AIC
end outfile

gnuplot d_infl --time-series --with-lines --output=phillips-d_infl.pdf
outfile phillips-adf-d_infl.txt
  adf 3 infl --c --difference --test-down=AIC
end outfile

gnuplot paro --time-series --with-lines --output=phillips-paro.pdf
outfile phillips-adf-paro.txt
  adf 3 paro --c --test-down=AIC
end outfile

gnuplot paroc --time-series --with-lines --output=phillips-paroc.pdf
outfile phillips-adf-paroc.txt
  adf 3 paroc --c --test-down=AIC
end outfile

outfile phillips-static.txt
  ols infl 0 covid paro --robust
end outfile

scalar Lstar = -3
scalar BICstar = $huge
outfile phillips-lags.txt
  loop for (L = Lstar; L <= 0; L += 1)
    ols d_infl 0 covid paroc(0 to L) --quiet
    printf "Lags = %d; BIC = %.6g\n", -L, $bic
    if $bic < BICstar
       Lstar = L
       BICstar = $bic
    endif
  endloop

  printf "\nL* = %d; BIC* = %.6g\n", -Lstar, BICstar
end outfile


outfile phillips-dyn.txt
  ols d_infl 0 covid paroc(0 to Lstar) --robust
end outfile

scalar Ld = Lstar + 1
outfile phillips-lr-mult.txt
  ols d_infl 0 covid d_paroc(0 to Ld) paroc(Lstar) --robust
end outfile

outfile phillips-lr-mult-0.txt
  ols d_infl 0 covid d_paroc(0 to Ld) --robust
end outfile
